#### This is for string "robot_status".
#### Before this, any field we need, the robot will set it.
#### For example, there is a field "need_reset" in the string.
#### We sure can derive this field from the status number.
####
#### Tire of changing robot code for adding new field,
#### a wrapper will be added to derive all those fields from
#### the status number.

package provide DCSRobotStatus 1.0  

package require DCSString

class DCS::RobotStatus {
	inherit DCS::String

	public method setContents
	public method configureString

    public method getNoPin { } {
        set statusNum [lindex $_contents 1]
        if {![string is integer -strict $statusNum]} {
            return 0
        }
        set noPinMask 136315906
        if {($statusNum & $noPinMask) == $noPinMask} {
            return 1
        } else {
            return 0
        }
    }

    ### return true if no pin or beamline alignment pin is on the goniometer.
    public method getOKToAlignBeam { } {
        set mounted [lindex $_contents 15]
        if {$mounted == "" || $mounted == "b 0 T" || $mounted == "b 1 T"} {
            return 1
        } else {
            return 0
        }
    }
    public method getOKToDismountTool { } {
        set mounted [lindex $_contents 15]
        if {[llength $mounted] < 3} {
            return 0
        }
        foreach {cas row col} $mounted break
        if {$cas == "b" && $col == "T"} {
            return 1
        }
        return 0
    }

	constructor { args } {
	    # call base class constructor
	    ::DCS::Component::constructor { \
		    contents { getContents } \
            noPinOnGoniometer { getNoPin } \
            OKToAlignBeam     { getOKToAlignBeam } \
            OKToDismountTool  { getOKToDismountTool } \
		 }
	} {
        ###following are the old fields generated by robotDHS
        createAttributeFromField status_num 1
        createAttributeFromField need_reset 3
        createAttributeFromField need_cal 5
        createAttributeFromField robot_state 7
        createAttributeFromField warning 9
        createAttributeFromField cal_msg 11
        createAttributeFromField cal_step 13
        createAttributeFromField mounted 15
        createAttributeFromField pin_lost 17
        createAttributeFromField pin_mounted 19
        createAttributeFromField in_manual 21
        createAttributeFromField need_mag_cal 23
        createAttributeFromField need_cas_cal 25
        createAttributeFromField need_clear 27
        createAttributeFromField in_tool 29
        createAttributeFromField extra_status_num 31
        createAttributeFromField in_barcode_setup 33
        createAttributeFromField in_barcode_read  35

		eval configure $args
		announceExist
	}
}
body DCS::RobotStatus::configureString { message_ } {
    #puts "RobotStatus: config $message_"

	configure -controller  [lindex $message_ 2] 
	set contents [lrange $message_ 3 end]

    setContents normal $contents
}

body DCS::RobotStatus::setContents { status_ contents_ } {
    #puts "RobotStatus setContents: $status_ $contents_"
    set lastResult $status_
    if {$lastResult == "normal"} {
        set _contents $contents_
	    #inform that new configuration is available
	    updateRegisteredComponents contents
	    updateRegisteredComponents noPinOnGoniometer
	    updateRegisteredComponents OKToAlignBeam
	    updateRegisteredComponents OKToDismountTool

	    updateRegisteredFieldListeners
    }

	recalcStatus
}
