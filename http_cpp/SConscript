#Main Sconscript for the ---http_cpp--- module
# An experimental build system used at LBNL (Nick Sauter) to compile the SSRL
# packages for WebIce.  Build tools include SCons & cctbx, not gmake.

import libtbx.load_env
import os
Import("env_base", "env_etc")

env_etc.http_cpp_dist = libtbx.env.dist_path("http_cpp")

env_etc.http_cpp_include = env_etc.norm_join(env_etc.http_cpp_dist,"src")
env_etc.http_cpp_common_includes = [
  env_etc.http_cpp_include,
  env_etc.xos_cpp_include,
  env_etc.logging_include,
  env_etc.xos_include,
]

env = env_base.Copy(
  CXXFLAGS=env_etc.cxxflags_base,
  SHCXXFLAGS=env_etc.cxxflags_base,
  SHLINKFLAGS=env_etc.shlinkflags,
)

env_etc.include_registry.append(
  env=env,
  paths=env_etc.http_cpp_common_includes)

env_http_cpp = env.Copy(
  LIBS = env_etc.libm + ["rt","pthread","c"],
)

arch_dependent_flags={"irix_CC":["-DIRIX","-DPTHREADS","-MP:dsm=off"],
                      "unix_gcc":["-DLINUX","-fexceptions","-pthread"], 
                      "tru64_cxx":["-DDEC_UNIX","-pthread"]}

env_http_cpp.Append(
CXXFLAGS=["-c","-D_REENTRANT"] +
  arch_dependent_flags[env_etc.compiler])

env_http_cpp.Append(LIBPATH=["#lib",])

lib_http_cpp_sources = [
  "src/InetdServer.cxx",
  "src/HttpServerFactory.cxx",
  "src/HttpMessage.cxx",
  "src/HttpRequest.cxx",
  "src/HttpUtil.cxx",
  "src/HttpResponse.cxx",
  "src/HttpClientImp.cxx",
  "src/HttpClientSSLImp.cxx",
  "src/SSLCommon.cxx",
  "src/HttpCookie.cxx",
  "src/SocketServer.cxx",
]

builder = env_http_cpp.StaticLibrary
builder(
    target="#lib/http_cpp",
    source=lib_http_cpp_sources,
)
